apply plugin: 'com.android.library'
apply plugin: 'maven-publish' // 确保添加了这个插件
android {
    compileSdk = 33
    namespace = "shape.meng.com.shape"
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 110
        versionName "1.1.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
        checkReleaseBuilds false
        disable 'UnusedResources' // 禁用未使用资源的检查
        quiet true
        disable 'TypographyFractions','TypographyQuotes'
        // 可选：配置 lint 输出位置
        // textOutput "lint-results.txt"
    }

    // 确保有 release 变体，并且发布相关文件
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}
configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'org.jetbrains.kotlin' && details.requested.name.contains('kotlin-stdlib')) {
            details.useVersion '1.8.22'
        }
    }
}
// 在 afterEvaluate 中配置 publishing，确保 Android 项目构建完成后才执行
afterEvaluate {
    publishing {
        publications {
            // 创建一个名为 'release' 的 MavenPublication
            create("release", MavenPublication) {
                // 从 Android 的 `components` 中选择需要发布的组件
                from(components.getByName("release"))

                groupId = 'mengyueyao'
                artifactId = 'shapewidget'
                version = '1.2.8'
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.4.1'
    testImplementation 'junit:junit:4.13.2'
}
